/*
 * Copyright 2020 ZUP IT SERVICOS EM TECNOLOGIA E INOVACAO SA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import br.com.zup.beagle.Dependencies

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.4.10"
        classpath "org.jetbrains.dokka:dokka-gradle-plugin:0.10.1"
        classpath 'com.vanniktech:gradle-maven-publish-plugin:0.12.0'
    }
}

plugins {
    id "org.jetbrains.kotlin.jvm" version "1.3.72" apply false
    id "io.gitlab.arturbosch.detekt" version "1.14.1" apply false
    id "br.com.zup.beagle.dependencies" apply false
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"
    apply plugin: "kotlin"
    apply plugin: "br.com.zup.beagle.dependencies"

    repositories {
        jcenter()
    }

    dependencies {
        implementation Dependencies.GeneralLibraries.kotlin
    }

    detekt {
        config = files("$rootDir/../config/detekt-default-config.yml")
    }

    tasks.withType(PublishToMavenRepository) {
        onlyIf {
            Arrays.asList("backend", "framework", "starter").any {
                publication.artifactId.contains(it)
            }
        }
    }

    compileKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            javaParameters = true
        }
    }

    compileTestKotlin {
        kotlinOptions {
            jvmTarget = JavaVersion.VERSION_1_8
            javaParameters = true
        }
    }
}