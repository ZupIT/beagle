/*
 * Copyright 2020 ZUP IT SERVICOS EM TECNOLOGIA E INOVACAO SA
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

import br.com.zup.beagle.Dependencies
import org.apache.tools.ant.taskdefs.condition.Os

apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-android-extensions'

android {
    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_1_8
        targetCompatibility = JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_1_8
    }

    compileSdkVersion Dependencies.Versions.compileSdk

    defaultConfig {
        applicationId "br.com.zup.beagle.appiumapp"
        minSdkVersion Dependencies.Versions.targetSdk
        targetSdkVersion Dependencies.Versions.compileSdk
        versionCode Dependencies.Releases.versionCode
        versionName "${Dependencies.Releases.beagleVersionName}"
    }

    sourceSets {
        debug.java.srcDirs += "src/debug/kotlin"
        main.java.srcDirs += "src/main/kotlin"
        test.java.srcDirs += "src/test/kotlin"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {

    implementation "br.com.zup.beagle:android:1.0.0-automated"
    kapt "br.com.zup.beagle:android-processor:1.0.0-automated"

    implementation Dependencies.GsonLibraries.gson
    implementation Dependencies.AndroidxLibraries.coreKtx
    implementation Dependencies.AndroidxLibraries.appcompat
    implementation Dependencies.GeneralLibraries.kotlinCoroutines
    implementation Dependencies.GoogleLibraries.materialDesign
}


task buildAndPublishBeagleAndroidLocally{
    doLast{
        def workingDirVar = "${projectDir}/../../../../android"
        def executableVar = ''

        if (Os.isFamily(Os.FAMILY_WINDOWS)) {
            executableVar = 'gradlew.bat'
        }else{
            executableVar = './gradlew'
        }

        exec {
            workingDir "${workingDirVar}"
            executable "${executableVar}"
            args "assemble"
        }

        exec {
            workingDir "${workingDirVar}"
            executable "${executableVar}"
            args 'publishToMavenLocal'
        }
    }
}
preBuild.dependsOn buildAndPublishBeagleAndroidLocally

